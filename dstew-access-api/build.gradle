plugins {
    id 'org.openapi.generator' version '7.12.0'
    id 'uk.gov.laa.ccms.springboot.laa-ccms-spring-boot-gradle-plugin'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
}

sourceSets.main.java.srcDirs += ['generated/src/main/java']

checkstyleMain.exclude("*")

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/dstew-access-api/open-api-specification.yml".toString()
    outputDir = "$rootDir/dstew-access-api/generated".toString()
    apiPackage = "uk.gov.justice.laa.dstew.access.api"
    invokerPackage = "uk.gov.justice.laa.dstew.access.invoker"
    modelPackage = "uk.gov.justice.laa.dstew.access.model"
    configOptions = [
            generateBuilders    : "true",
            interfaceOnly       : "true", // This will only generate interfaces, not implementations
            serializableModel   : "true",
            skipDefaultInterface: "true",
            useJakartaEe        : "true",
            useSpringBoot3      : "true",
            useTags             : "true",
    ]
    globalProperties = [
            models              : "", // omitting `supportingFiles` prevents generation of
            apis                : "", // pom.xml, README.md, ApiUtil.java, etc.
    ]
}

compileJava.dependsOn('openApiGenerate')

clean {
    delete("$rootDir/dstew-access-api/generated")
}

// disable for overall project when running a build
tasks.named('bootJar') {
    enabled = false
}

tasks.named('bootRun') {
    enabled = false
}